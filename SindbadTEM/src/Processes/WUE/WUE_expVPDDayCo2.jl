export WUE_expVPDDayCo2

#! format: off
@bounds @describe @units @timescale @with_kw struct WUE_expVPDDayCo2{T1,T2,T3,T4,T5} <: WUE
    WUE_one_hpa::T1 = 9.2 | (2.0, 20.0) | "WUE at 1 hpa VPD" | "gC/mmH2O" | ""
    κ::T2 = 0.4 | (0.06, 0.7) | "" | "kPa-1" | ""
    base_ambient_CO2::T3 = 380.0 | (300.0, 500.0) | "" | "ppm" | ""
    sat_ambient_CO2::T4 = 500.0 | (10.0, 2000.0) | "" | "ppm" | ""
    kpa_to_hpa::T5 = 10.0 | (-Inf, Inf) | "unit conversion kPa to hPa" | "" | ""
end
#! format: on

function compute(params::WUE_expVPDDayCo2, forcing, land, helpers)
    ## unpack parameters and forcing
    @unpack_WUE_expVPDDayCo2 params
    @unpack_nt f_VPD_day ⇐ forcing

    ## unpack land variables
    @unpack_nt begin
        ambient_CO2 ⇐ land.states
    end

    ## calculate variables
    WUENoCO2 = WUE_one_hpa * exp(κ * -(f_VPD_day))
    fCO2_CO2 = one(ambient_CO2) + (ambient_CO2 - base_ambient_CO2) / (ambient_CO2 - base_ambient_CO2 + sat_ambient_CO2)
    WUE = WUENoCO2 * fCO2_CO2

    ## pack land variables
    @pack_nt WUENoCO2 ⇒ land.diagnostics
    @pack_nt WUE ⇒ land.diagnostics
    return land
end

purpose(::Type{WUE_expVPDDayCo2}) = "Calculates WUE as a function of WUE at 1 hPa, daily mean VPD, and an exponential CO₂ relationship."

@doc """

$(getModelDocString(WUE_expVPDDayCo2))

---

# Extended help

*References*

*Versions*
 - 1.0 on 31.03.2021 [skoirala | @dr-ko]

*Created by*
 - skoirala | @dr-ko
"""
WUE_expVPDDayCo2
