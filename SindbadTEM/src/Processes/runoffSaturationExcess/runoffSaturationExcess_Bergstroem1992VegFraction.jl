export runoffSaturationExcess_Bergstroem1992VegFraction

#! format: off
@bounds @describe @units @timescale @with_kw struct runoffSaturationExcess_Bergstroem1992VegFraction{T1,T2} <: runoffSaturationExcess
    β::T1 = 3.0 | (0.1, 10.0) | "linear scaling parameter to get the berg parameter from vegFrac" | "" | ""
    β_min::T2 = 0.1 | (0.08, 0.120) | "minimum effective β" | "" | ""
end
#! format: on

function compute(params::runoffSaturationExcess_Bergstroem1992VegFraction, forcing, land, helpers)
    ## unpack parameters
    @unpack_runoffSaturationExcess_Bergstroem1992VegFraction params

    ## unpack land variables
    @unpack_nt begin
        (WBP, frac_vegetation) ⇐ land.states
        w_sat ⇐ land.properties
        soilW ⇐ land.pools
        ΔsoilW ⇐ land.pools
    end
    tmp_smax_veg = sum(w_sat)
    tmp_soilW_total = sum(soilW + ΔsoilW)
    # get the berg parameters according the vegetation fraction
    β_veg = max(β_min, β * frac_vegetation) # do this?
    # calculate land runoff from incoming water & current soil moisture
    tmp_sat_exc_frac = clampZeroOne((tmp_soilW_total / tmp_smax_veg)^β_veg)
    sat_excess_runoff = WBP * tmp_sat_exc_frac
    # update water balance pool
    WBP = WBP - sat_excess_runoff

    ## pack land variables
    @pack_nt begin
        sat_excess_runoff ⇒ land.fluxes
        β_veg ⇒ land.runoffSaturationExcess
        WBP ⇒ land.states
    end
    return land
end

purpose(::Type{runoffSaturationExcess_Bergstroem1992VegFraction}) = "Saturation excess runoff using the Bergström method with parameters scaled by vegetation fraction."

@doc """

$(getModelDocString(runoffSaturationExcess_Bergstroem1992VegFraction))

---

# Extended help

*References*
 - Bergström, S. (1992). The HBV model–its structure & applications. SMHI.

*Versions*
 - 1.0 on 18.11.2019 [ttraut]: cleaned up the code  
 - 1.1 on 27.11.2019 [skoirala | @dr-ko]: changed to handle any number of soil layers
 - 1.2 on 10.02.2020 [ttraut]: modyfying variable name to match the new SINDBAD version

*Created by*
 - ttraut
"""
runoffSaturationExcess_Bergstroem1992VegFraction
